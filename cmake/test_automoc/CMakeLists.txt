cmake_minimum_required(VERSION 3.0.2)

project(test_automoc CXX)

# Find the QtWidgets library
find_package(Qt5Widgets)

#_______________________________________________

# Example using qt5_wrap
qt5_wrap_cpp(
  QT_MOC_OUTFILES
  ${CMAKE_SOURCE_DIR}/include/test_q_object.h
  )
add_executable(helloworld_qt5_wrap src_noinclude/helloworld.cpp ${QT_MOC_OUTFILES})
target_link_libraries(helloworld_qt5_wrap Qt5::Widgets)

# header is in the source-tree include directory and the
# moc-generated result is part of the source files so does
# not need to be part of INCLUDE_DIRECTORIES.
set_target_properties(helloworld_qt5_wrap
  PROPERTIES
  INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/include"
  )

#_______________________________________________

# Example using automoc while adding header in separate directory to target source files

add_executable(helloworld_automoc_noinclude src_noinclude/helloworld.cpp ${CMAKE_SOURCE_DIR}/include/test_q_object.h)
# Use the Widgets module from Qt 5.
target_link_libraries(helloworld_automoc_noinclude Qt5::Widgets)

# Set automoc to ON.
# Also, note that the required test_q_object.h header is in the
# source-tree include directory and automoc result is automatically
# added to the target source files so no need to specify the
# build-tree for the current directory as an include directory.
set_target_properties(helloworld_automoc_noinclude
  PROPERTIES
  AUTOMOC ON
  INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/include"
  )

#_______________________________________________

# Example using automoc with header with same basename in same directory, but no include
# of automoc result.

add_executable(helloworld_automoc_same_noinclude src_same_noinclude/helloworld.cpp )
# Use the Widgets module from Qt 5.
target_link_libraries(helloworld_automoc_same_noinclude Qt5::Widgets)

# Set automoc to ON.
# Also, note that the required helloworld.h header is in the
# same source-tree directory and the automoc result is automatically
# added to the target source files so no need to specify the
# build-tree for the current directory as an include directory.
set_target_properties(helloworld_automoc_same_noinclude
  PROPERTIES
  AUTOMOC ON
  INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/src_same_noinclude"
  )

#_______________________________________________

# Example using automoc with header with different basename in same directory and with
# specific include of moc result that is generated by automoc.

add_executable(helloworld_automoc_same_include src_same_include/helloworld.cpp )
# Use the Widgets module from Qt 5.
target_link_libraries(helloworld_automoc_same_include Qt5::Widgets)

# Set automoc to ON.
# Also, note that the required helloworld.h header is in the same
# source-tree directory and the source file #includes the specific moc
# result produced by automoc so need to specify the build-tree for the
# current directory as an include directory.
set_target_properties(helloworld_automoc_same_include
  PROPERTIES
  AUTOMOC ON
  INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/src_same_include;${CMAKE_CURRENT_BINARY_DIR}"
  )

#_______________________________________________

# Second (identical except for target name to demonstrate nameclash)
# example using automoc with header with different basename in same directory and with
# specific include of moc result that is generated by automoc.

add_executable(helloworld_automoc_same_include1 src_same_include/helloworld.cpp )
# Use the Widgets module from Qt 5.
target_link_libraries(helloworld_automoc_same_include1 Qt5::Widgets)

# Set automoc to ON.
# Also, note that the required helloworld.h header is in the same
# source-tree directory and the source file #includes the specific moc
# result produced by automoc so need to specify the build-tree for the
# current directory as an include directory.
set_target_properties(helloworld_automoc_same_include1
  PROPERTIES
  AUTOMOC ON
  INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/src_same_include;${CMAKE_CURRENT_BINARY_DIR}"
  )

#_______________________________________________

# Third (identical except for target name to demonstrate nameclash)
# example using automoc with header with different basename in same directory and with
# specific include of moc result that is generated by automoc.  However, instead
# of using the same directory for the header and implementation file
# for this target we use a distinct src_same_include_alt directory
# that contains an identical copy of the same header and implementation
# file.

add_executable(helloworld_automoc_same_include2 src_same_include_alt/helloworld.cpp )
# Use the Widgets module from Qt 5.
target_link_libraries(helloworld_automoc_same_include2 Qt5::Widgets)

# Set automoc to ON.
# Also, note that the required helloworld.h header is in the same
# source-tree directory and the source file #includes the specific moc
# result produced by automoc so need to specify the build-tree for the
# current directory as an include directory.
set_target_properties(helloworld_automoc_same_include2
  PROPERTIES
  AUTOMOC ON
  INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/src_same_include_alt;${CMAKE_CURRENT_BINARY_DIR}"
  )
