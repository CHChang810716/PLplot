
AC_INIT(configure.in)

dnl Version number

VERSION=0.1.0
AC_SUBST(VERSION)

dnl Controlling were the HTML files should go

PLPLOTDOC_HTML=plplotdoc-html
AC_SUBST(PLPLOTDOC_HTML)

MANIFEST=MANIFEST
AC_SUBST(MANIFEST)

PLPLOTDOC=plplotdoc
AC_SUBST(PLPLOTDOC)


dnl System wide XML declaration

XML_DECL=/usr/lib/sgml/declaration/xml.dcl
AC_ARG_WITH(xml-declaration, [
  --with-xml-declaration=FILE     System wide file containing the SGML 
                                  declaration for XML.  Must be a absolute
                                  file name.
                                  Default: /usr/lib/sgml/declaration/xml.dcl],
  [XML_DECL=$withval])
AC_SUBST(XML_DECL)


dnl Jade output log

jadelog=jadeout.log
rm -f $jadelog
JADELOG=$jadelog
AC_SUBST(JADELOG)


dnl DTD definitions.
dnl
dnl The following public identifiers should correspond to those in the 
dnl SGML source files. 

DSSSL_DTD_PUBID="-//James Clark//DTD DSSSL Style Sheet//EN"
DB_SS_HTML_PUBID="-//Norman Walsh//DOCUMENT DocBook HTML Stylesheet//EN"
DB_SS_PRINT_PUBID="-//Norman Walsh//DOCUMENT DocBook Print Stylesheet//EN"
DOCBOOK_DTD_PUBID="-//Norman Walsh//DTD DocBk XML V3.1.3//EN"

AC_SUBST(DSSSL_DTD_PUBID)
AC_SUBST(DB_SS_HTML_PUBID)
AC_SUBST(DB_SS_PRINT_PUBID)
AC_SUBST(DOCBOOK_DTD_PUBID)

dnl Utility macros

dnl CHECK_PROG(program, url)
AC_DEFUN(CHECK_PROG, [

define(PROG, translit($1, [a-z], [A-Z]))

AC_ARG_WITH($1, [
  --with-$1=PATH/]PROG[		Alternative $1 program],
  [prog_$1=$withval], 
  [prog_$1=$1])
AC_CHECK_PROG(has_$1, [$prog_$1], yes, no)
PROG=$prog_$1
AC_SUBST(PROG)
if test $has_$1 = no ; then
  for i in $2 ; do
    export $i=""
  done
fi
AC_OUTPUT_COMMANDS( [
if test $has_$1 = no ; then]
AC_MSG_WARN( [Program $1 not found.  
    Install the $1 program in your path or specify its
    location with the option --with-$1.  The $1 package can be 
    found at $3.])
[fi], has_$1=$has_$1)

])

PRINT=print
HTML=html


dnl CHECK_DTD(title, cache-id, dsssl_dtd, docbookb_ss_dtd, style_spec_use,
dnl           external_specification, docbook_dtd, jade_output_type, 
dnl           origin_package)
AC_DEFUN(CHECK_DTD, [
AC_CACHE_CHECK($1, pldb_cv_$2, [
echo -e "\nChecking for $1" >> $jadelog
sed -e "s|@DSSSL_DTD@|\"$DSSSL_DTD_PUBID\"|" -e "s|@DB_STYLE_SHEET@|$3|" \
  -e "s|@STYLE_SPEC_USE@|$4|" -e "s|@EXTERNAL_SPECIFICATION@|$5|" \
  < conf/test.dsl.in > conftest.dsl
sed "s|@DOCBOOK_DTD@|$6|" < conf/test.sgml.in > conftest.sgml
$prog_jade $SGML_CATALOGS -d conftest.dsl -t $7 $XML_DECL \
   conftest.sgml > $jadelog.x 2>&1
jade_result=`egrep ":(E|W):" $jadelog.x`
cat $jadelog.x >> $jadelog
rm -f $jadelog.x
if test "$jade_result" = "" ; then 
  pldb_cv_$2=yes
else 
  pldb_cv_$2=no
fi
])
if test $pldb_cv_$2 = no ; then
  for i in $11 ; do
    export $i=""
  done
fi
AC_OUTPUT_COMMANDS([
if test $pldb_cv_$2 = no ; then]
  AC_MSG_WARN([Could not find $1.
    The following SGML public identifier could not be found in any of 
    the default SGML catalogs:
        $dtd_$2
    Its system identifier[,] i.e. a file usually called 
        \"$9\" 
    is distributed with the $10 package.  Install it in your 
    system and put the correct entry in the catalog file.
    You might also use the configure option --with-sgml-catalog to specify 
    alternative SGML catalogs.])
[fi
], [
pldb_cv_$2=$pldb_cv_$2
dtd_$2="$8"
])
])

dnl SGML catalogs

AC_ARG_WITH(sgml-catalogs, [
  --with-sgml-catalogs=CATALOGS   SGML catalogs in a colon (:) separated
                                  list.  Must contain only existent files.],
  [SGML_CATALOGS=$withval],
  [SGML_CATALOGS=""])

[
for i in `echo $SGML_CATALOGS | sed "s/:/ /g"` ; do
  if test ! -f $i ; then ]
AC_MSG_ERROR([Catalog file $i is not valid.
    Specify only existent files with option --with-sgml-catalogs.])[
  fi
done

if test ! "$SGML_CATALOGS" = "" ;then
  SGML_CATALOGS="-c `echo $SGML_CATALOGS | sed 's/:/ -c /g'`"
fi
]

AC_SUBST(SGML_CATALOGS)

dnl HTML file extension

HTML_EXT=html
AC_ARG_WITH(html-extension, [
  --with-html-extension=EXT       File extension for the generated HTML      
                                  files. (Default value: html)],
  [HTML_EXT=$withval])
AC_SUBST(HTML_EXT)


dnl Check programs

CHECK_PROG(jade, [HTML PRINT], http://www.jclark.com/jade/)
CHECK_PROG(jadetex, PRINT, http://www.tug.org/applications/jadetex/)
CHECK_PROG(perl, [HTML PRINT], http://www.perl.com)
CHECK_PROG(mogrify, HTML, ftp://ftp.wizards.dupont.com/pub/ImageMagick/)
CHECK_PROG(latex, PRINT, http://www.ctan.org)
CHECK_PROG(dvips, PRINT, http://www.ctan.org)

dnl Check public identifiers

CHECK_DTD(
  [DSSSL Style Sheet DTD],
  [dsssl_dtd],
  [],
  [],
  [],
  [[[<!ELEMENT book - O (#PCDATA)>]]],
  [sgml],
  [\"$DSSSL_DTD_PUBID\"],
  [style-sheet.dtd],
  [jade],
  [PRINT HTML])

CHECK_DTD(
  [DocBook HTML Stylesheet],
  [html_ss],
  [[[<!ENTITY dbstyle PUBLIC \"$DB_SS_HTML_PUBID\" CDATA DSSSL>]]],
  [use="docbook"],
  [<external-specification id="docbook" document="dbstyle">],
  [[[<!ELEMENT book - O (#PCDATA)>]]],
  [sgml],
  [\"$DB_SS_HTML_PUBID\"],
  [html/docbook.dsl],
  [docbook-stylesheets],
  [HTML])
  
CHECK_DTD(
  [DocBook Print Stylesheet],
  [print_ss],
  [[[<!ENTITY dbstyle PUBLIC \"$DB_SS_PRINT_PUBID\" CDATA DSSSL>]]],
  [use="docbook"],
  [<external-specification id="docbook" document="dbstyle">],
  [[[<!ELEMENT book - O (#PCDATA)>]]],
  [tex],
  [\"$DB_SS_PRINT_PUBID\"],
  [print/docbook.dsl],
  [docbook-stylesheets],
  [PRINT])

CHECK_DTD(
  [DocBook DTD],
  [db_dtd],
  [],
  [],
  [],
  [PUBLIC \"$DOCBOOK_DTD_PUBID\"],
  [sgml],
  [\"$DOCBOOK_DTD_PUBID\"],
  [docbookx.dtd],
  [docbook-xml (DTD version 3.1.3)],
  [PRINT HTML])

AC_SUBST(HTML)
AC_SUBST(PRINT)

AC_OUTPUT_COMMANDS( [
if test "$print" = "" ; then
  echo
  echo '  ******************************************************************'
  echo '  * Due to some lacking dependencies (see warning messages above), *'
  echo '  * it will not be possible to build a printed version of the      *'
  echo '  * documentation.                                                 *'
  echo '  ******************************************************************'
fi
if test "$html" = "" ; then
  echo
  echo '  ******************************************************************'
  echo '  * Due to some lacking dependencies (see warning messages above), *'
  echo '  * it will not be possible to build a HTML version of the         *'
  echo '  * documentation.                                                 *'
  echo '  ******************************************************************'
fi], [
print=$PRINT; html=$HTML
])

AC_OUTPUT([Makefile plplotdoc.sgml plplotdoc-html.dsl plplotdoc-print.dsl 
           inline-greek.pl eps2jpeg.pl tex2eps.pl])
