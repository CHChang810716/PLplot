# Top-level CMakeLists.txt for installed PLplot examples
###
### Process this file with cmake to produce Makefile
###
# Copyright (C) 2009 Alan W. Irwin
#
# This file is part of PLplot.
#
# PLplot is free software; you can redistribute it and/or modify
# it under the terms of the GNU Library General Public License as published
# by the Free Software Foundation; version 2 of the License.
#
# PLplot is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Library General Public License for more details.
#
# You should have received a copy of the GNU Library General Public License
# along with PLplot; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA

project(installed_plplot_examples NONE)

CMAKE_MINIMUM_REQUIRED(VERSION 2.6.4 FATAL_ERROR)
# Location where PLplot cmake build system first looks for cmake modules.
set(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake/modules)

#Configure variables and enable languages as needed.
include(plplot_configure)

include(export_plplot)
add_subdirectory(c)
get_property(targets_examples_c GLOBAL PROPERTY TARGETS_examples_c)
set(language_info_LIST c:c)

if(ENABLE_ada)
  add_subdirectory(ada)
  get_property(targets_examples_ada GLOBAL PROPERTY TARGETS_examples_ada)
  list(APPEND language_info_LIST ada:a)
endif(ENABLE_ada)

if(ENABLE_cxx)
  add_subdirectory(c++)
  get_property(targets_examples_cxx GLOBAL PROPERTY TARGETS_examples_cxx)
  list(APPEND language_info_LIST cxx:cxx)
endif(ENABLE_cxx)

if(ENABLE_d)
  add_subdirectory(d)
  get_property(targets_examples_d GLOBAL PROPERTY TARGETS_examples_d)
  list(APPEND language_info_LIST d:d)
endif(ENABLE_d)

if(ENABLE_f77)
  add_subdirectory(f77)
  get_property(targets_examples_f77 GLOBAL PROPERTY TARGETS_examples_f77)
  list(APPEND language_info_LIST f77:f)
endif(ENABLE_f77)

if(ENABLE_f95)
  add_subdirectory(f95)
  get_property(targets_examples_f95 GLOBAL PROPERTY TARGETS_examples_f95)
  list(APPEND language_info_LIST f95:f95)
endif(ENABLE_f95)

if(ENABLE_java)
  add_subdirectory(java)
  get_property(targets_examples_java GLOBAL PROPERTY TARGETS_examples_java)
  list(APPEND language_info_LIST java:j)
endif(ENABLE_java)

if(ENABLE_lua)
  add_subdirectory(lua)
  get_property(targets_examples_lua GLOBAL PROPERTY TARGETS_examples_lua)
  list(APPEND language_info_LIST lua:lua)
endif(ENABLE_lua)

if(ENABLE_ocaml)
  add_subdirectory(ocaml)
  get_property(targets_examples_ocaml GLOBAL PROPERTY TARGETS_examples_ocaml)
  list(APPEND language_info_LIST ocaml:ocaml)
endif(ENABLE_ocaml)

if(ENABLE_octave)
  list(APPEND language_info_LIST octave:o)
  add_subdirectory(octave)
  execute_process(
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
    ${CMAKE_CURRENT_SOURCE_DIR}/lena.img
    ${CMAKE_CURRENT_BINARY_DIR}/lena.img
    )
  execute_process(
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
    ${CMAKE_CURRENT_SOURCE_DIR}/octave/lena.img
    ${CMAKE_CURRENT_BINARY_DIR}/octave/lena.img
    )
endif(ENABLE_octave)

if(ENABLE_pdl)
  list(APPEND language_info_LIST perl:pdl)
endif(ENABLE_pdl)

if(ENABLE_python)
  add_subdirectory(python)
  get_property(targets_examples_python GLOBAL PROPERTY TARGETS_examples_python)
  list(APPEND language_info_LIST python:p)
endif(ENABLE_python)

if(ENABLE_tcl)
  add_subdirectory(tcl)
  get_property(targets_examples_tcl GLOBAL PROPERTY TARGETS_examples_tcl)
  list(APPEND language_info_LIST tcl:t)
endif(ENABLE_tcl)

execute_process(
  COMMAND ${CMAKE_COMMAND} -E copy_if_different
  ${CMAKE_CURRENT_SOURCE_DIR}/lena.pgm 
  ${CMAKE_CURRENT_BINARY_DIR}/lena.pgm
  )

set(output_LIST)
set(targets_LIST)
foreach(language_info ${language_info_LIST})
  string(REGEX REPLACE "^(.*):.*$" "\\1" language ${language_info})
  string(REGEX REPLACE "^.*:(.*)$" "\\1" suffix ${language_info})

  # Will change from x03 to x01 as the representative result below when
  # standard example 1 is implemented in d.
  add_custom_command(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/x03${suffix}.psc
    COMMAND ${CMAKE_COMMAND} -E echo "Generate ${language} results for psc device"
    COMMAND SRC_EXAMPLES_DIR=${CMAKE_CURRENT_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR}/plplot-test.sh --verbose --front-end=${language} --device=psc
    DEPENDS
    ${CMAKE_CURRENT_SOURCE_DIR}/plplot-test.sh
    )
  list(APPEND output_LIST ${CMAKE_CURRENT_BINARY_DIR}/x03${suffix}.psc)
  if(targets_examples_${language})
    list(APPEND targets_LIST ${targets_examples_${language}})
  endif(targets_examples_${language})
endforeach(language_info ${language_info_LIST})

add_custom_command(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/compare
  COMMAND ${CMAKE_COMMAND} -E touch ${CMAKE_CURRENT_BINARY_DIR}/compare
  COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/test_diff.sh
  DEPENDS ${output_LIST}
  )

add_custom_target(test_noninteractive 
DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/compare)
add_dependencies(test_noninteractive ${targets_LIST})

if(ENABLE_tk)
  add_subdirectory(tk)
  get_property(targets_examples_tk GLOBAL PROPERTY TARGETS_examples_tk)
endif(ENABLE_tk)
    
