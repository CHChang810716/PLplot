divert(-1)dnl
!***********************************************************************
! $Id$
!
!***********************************************************************
! Uses implicit_none, REAL, and REAL_FUN from fmacs.m4.
divert(1)dnl
include(fmacs.m4)dnl
! Illustration of 1-1 scaling for polar plot
      implicit_none

      character*3 text
      REAL x0(0:360), y0(0:360)
      REAL x(0:360), y(0:360), dtr, theta, dx, dy, r
      integer i, j, nsp

      dtr = 3.141592654/180.0
      do 1 i=0,360
        x0(i) = cos(dtr * REAL_FUN (i))
        y0(i) = sin(dtr * REAL_FUN (i))
    1 continue

! Ask user to specify the output device.

      call plinit()

! Set up viewport and window, but do not draw box

      call plenv(REAL_FUN (-1.3), REAL_FUN (1.3), REAL_FUN (-1.3),
     &  REAL_FUN (1.3), 1, -2)
      do 2 i = 1,10
        do 3 j = 0,360
          x(j) = 0.1*i*x0(j)
          y(j) = 0.1*i*y0(j)
    3   continue

! Draw circles for polar grid

        call plline(361,x,y)
    2 continue

      call plcol(2)
      do 4 i = 0,11
        theta = 30.0*i
        dx = cos(dtr*theta)
        dy = sin(dtr*theta)

! Draw radial spokes for polar grid

        call pljoin(REAL_FUN (0.0), REAL_FUN (0.0), dx, dy)
        write (text,'(i3)') nint(theta)

! Write labels for angle

        text = text(nsp(text):)
        if (dx.ge.0) then
          call plptex(dx, dy, dx, dy, REAL_FUN (-0.15), text)
        else
          call plptex(dx, dy, -dx, -dy, REAL_FUN (1.15), text)
        end if
    4 continue

! Draw the graph

      do 5 i=0,360
        r = sin(dtr*REAL_FUN (5*i))
        x(i) = x0(i) * r
        y(i) = y0(i) * r
    5 continue
      call plcol(3)
      call plline(361,x,y)

      call plcol(4)
      call plmtex
     &  ('t', REAL_FUN (2.0), REAL_FUN (0.5), REAL_FUN (0.5), 
     &  '#frPLPLOT Example 3 - r(#gh)=sin 5#gh')

! Close the plot at end

      call plend
      end

      integer function nsp(text)
!     ==================

! Find first non-space character
      implicit_none

      character*(*) text
      integer l, len

      l = len (text)
      nsp = 1
    1 if (text(nsp:nsp).eq.' ' .and. nsp.lt.l) then
        nsp = nsp+1
        goto 1
      end if
      end
