! Illustration of logarithmic axes, and redefinition of window
      implicit none
      real*8 PI
      parameter (PI = 3.1415926535897932384d0)

      real*8 freql(0:100),ampl(0:100),phase(0:100), freq, f0
      integer i
      call plinit()
      call pladv(0)
      call plfont(2)

      f0 = 1000.0d0
      do 1 i=0,100
        freql(i)=1.0d0+ dble (i)/20.0d0
        freq=10.0d0**freql(i)
        ampl(i)=20.0d0*log10(1.0d0/sqrt(1.0d0+(freq/f0)**2))
        phase(i)=-(180.0d0/PI)*atan(freq/f0)
    1 continue

      call plvpor(0.15d0, 0.85d0, 0.1d0, 0.9d0)
      call plwind(1.0d0, 6.0d0, -80.0d0, 0.0d0)
      call plcol(1)
      call plbox('bclnst', 0.0d0, 0, 'bnstv', 0.0d0, 0)
      call plcol(2)
      call plline(101,freql,ampl)
      call plcol(1)
      call plptex(5.0d0, -30.0d0, 1.0d0, -20.0d0,
     &  0.5d0, '-20 dB/decade')
      call plwind(1.0d0, 6.0d0, -100.0d0, 0.0d0)
      call plbox(' ', 0.0d0, 0, 'cmstv', 30.0d0, 3)
      call plcol(3)
      call plline(101,freql,phase)

      call plcol(1)
      call plmtex('b', 3.2d0, 0.5d0, 0.5d0, 'Frequency')
      call plmtex('t',2.0d0, 0.5d0, 0.5d0,
     &  'Single Pole Low-Pass Filter')
      call plcol(2)
      call plmtex('l', 5.0d0, 0.5d0, 0.5d0,
     &  'Amplitude (dB)')
      call plcol(3)
      call plmtex('r', 5.0d0, 0.5d0, 0.5d0,
     &  'Phase shift (degrees)')

      call plend
      end
