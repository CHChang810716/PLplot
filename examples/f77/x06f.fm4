c      $Id$
c      Displays the plotter symbols for PLPOIN
c
c      Copyright (C) 2004  Alan W. Irwin
c
c      This file is part of PLplot.
c
c      PLplot is free software; you can redistribute it and/or modify
c      it under the terms of the GNU General Library Public License as
c      published by the Free Software Foundation; either version 2 of the
c      License, or (at your option) any later version.
c
c      PLplot is distributed in the hope that it will be useful,
c      but WITHOUT ANY WARRANTY; without even the implied warranty of
c      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
c      GNU Library General Public License for more details.
c
c      You should have received a copy of the GNU Library General Public
c      License along with PLplot; if not, write to the Free Software
c      Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA

      implicit none
      integer i, j, k, font, kind_font, maxfont
      real*8 x, y

      character*3 text
      integer PL_PARSE_FULL
      parameter(PL_PARSE_FULL = 1)
c      Process command-line arguments
      call plparseopts(PL_PARSE_FULL)


c      Full sized page for display

      call plinit()

      do kind_font=1,2
        call plfontld( kind_font-1 )
        if (kind_font.eq.1) then
          maxfont=1
        else
          maxfont=4
        endif

        do font=1,maxfont

          call plfont( font )

          call pladv(0)

          call plcol0(2)

c         Set up viewport and window

          call plvpor(0.1d0, 1.0d0, 0.1d0, 0.9d0)
          call plwind(0.0d0, 1.0d0, 0.0d0, 1.3d0)

c         Draw the grid using plbox

          call plbox('bcg', 0.1d0, 0, 'bcg', 0.1d0, 0)

c         Write the digits below the frame

          call plcol0(15)
          do i=0,9
            write (text,'(i1)') i
            call plmtex('b', 1.5d0, (0.1d0*i+0.05d0), 0.5d0, text)
          enddo

          k=0
          do i=0,12

c           Write the digits to the left of the frame

            if (i.eq.0) then
              write (text,'(i1)') 10*i
            elseif(i.le.9) then
              write (text,'(i2)') 10*i
            else
              write (text,'(i3)') 10*i
            endif
            call plmtex('lv', 1.0d0, (1.0d0-(2*i+1)/26.0d0),
     &             1.0d0, text)
            do j=0,9
              x=0.1d0*j+0.05d0
              y=1.25d0-0.1d0*i

c             Display the symbols

              if (k.lt.128) call plpoin(1,x,y,k)
              k=k+1
            enddo
          enddo
          if (kind_font.eq.1) then
            call plmtex('t', 1.5d0, 0.5d0, 0.5d0,
     &        'PLplot Example 6 - plpoin symbols (compact)')
          else
            call plmtex('t', 1.5d0, 0.5d0, 0.5d0,
     &        'PLplot Example 6 - plpoin symbols (extended)')
          endif
        enddo
      enddo

      call plend
      end
