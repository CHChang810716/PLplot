! Displays the plotter symbols for PLPOIN
      implicit none
      integer i, j, k
      real*8 x, y

      character*3 text

! Full sized page for display

      call plinit()
      call pladv(0)

      call plcol(2)

! Set up viewport and window

      call plvpor(0.1d0, 1.0d0, 0.1d0, 0.9d0)
      call plwind(0.0d0, 1.0d0, 0.0d0, 1.3d0)

! Draw the grid using plbox

      call plbox('bcgt', 0.1d0, 0, 'bcgt', 0.1d0, 0)
      call plcol(15)

! Write the digits below the frame

      do 5 i=0,9
        write (text,'(i1)') i
        call plmtex('b', 1.5d0, dble (0.1d0*i+0.05d0),
     &    0.5d0, text)
    5 continue

      k=0
      do 1 i=0,12

! Write the digits to the left of the frame

        write (text,'(i3)') 10*i
        call plmtex('lv', 1.0d0, dble (1.0d0-(2*i+1)/26.0d0),
     &    1.0d0, text)
        do 2 j=0,9
          x=0.1d0*j+0.05d0
          y=1.25d0-0.1d0*i

! Display the symbols

          if (k.lt.128) call plpoin(1,x,y,k)
          k=k+1
    2   continue
    1 continue

      call plmtex('t', 1.5d0, 0.5d0, 0.5d0,
     &  'PLPLOT Example 6 - PLPOIN symbols')
      call plend
      end
