C      Displays the plotter symbols for PLSYM
      implicit none

      character*4 text

      integer i, j, k, l, base(17)
      data base /   0, 200, 500, 600, 700, 800, 900,
     &  2000,2100,2200,2300,2400,2500,2600,
     &  2700,2800,2900 /
      real*8 x, y


C      Full sized page for display

      call plinit()
      call plfontld(1)
      do l=1,17
        call pladv(0)

        call plcol0(2)

C        Set up viewport and window

        call plvpor(0.15d0, 0.95d0, 0.1d0, 0.9d0)
        call plwind(0.0d0, 1.0d0, 0.0d0, 1.0d0)

C        Draw the grid using plbox

        call plbox('bcgt', 0.1d0, 0,'bcgt', 0.1d0, 0)
        call plcol0(15)

C        Write the digits below the frame

        do  i=0,9
          write (text,'(i1)') i
          call plmtex('b', 1.5d0, (0.1d0*i+0.05d0),
     &                0.5d0, text)
        enddo
        k=0
        do i=0,9

C          Write the digits to the left of the frame

          write (text,'(i4)') base(l)+10*i
          call plmtex('lv', 1.0d0, (0.95d0-0.1d0*i),
     &      1.0d0, text)
          do j=0,9
            x=0.1d0*j+0.05d0
            y=0.95d0-0.1d0*i

C            Display the symbols

            call plsym(1,x,y,base(l)+k)
            k=k+1
          enddo
        enddo

        call plmtex('t', 1.5d0, 0.5d0, 0.5d0,
     &    'PLplot Example 7 - PLSYM symbols')
      enddo
      call plend
      end
