! Demonstration of 3-d plotting
!
      implicit none
      real*8 PI
      parameter (PI = 3.1415926535897932384d0)
      integer i, j, k, opt(4), npts
      parameter (npts=41)

      real*8 x(npts), y(npts), z(npts,npts), xx, yy, r

      character*80 title(4)
      real*8 alt(4),az(4)
      data opt /1,   2,   3,   3/
      data alt /60.0d0,30.0d0,60.0d0,60.0d0/
      data az  /30.0d0,60.0d0,120.0d0,160.0d0/
      data title /'#frPLPLOT Example 8 - Alt=60, Az=30, Opt=1',
     *            '#frPLPLOT Example 8 - Alt=30, Az=60, Opt=2',
     *            '#frPLPLOT Example 8 - Alt=60, Az=120, Opt=3',
     *            '#frPLPLOT Example 8 - Alt=60, Az=160, Opt=3'/

      do 1 i=1,npts
        x(i) = (i-1-(npts/2))/dble (npts/2)
        y(i) = (i-1-(npts/2))/dble (npts/2)
    1 continue

      do 2 i=1,npts
        xx = x(i)
        do 3 j=1,npts
          yy = y(j)
          r = sqrt(xx*xx + yy*yy)
          z(i,j) = exp(-r*r) * cos(2.0d0*PI*r)
    3   continue
    2 continue

      call plinit()
      do 5 k=1,4
         call pladv(0)
         call plcol(1)
         call plvpor(0.0d0, 1.0d0, 0.0d0, 0.9d0 )
         call plwind(-1.0d0, 1.0d0, -0.9d0, 1.1d0 )
         call plw3d(1.0d0, 1.0d0, 1.0d0, -1.0d0,
     &     1.0d0, -1.0d0, 1.0d0, -1.0d0,
     &     1.0d0, alt(k),az(k))
         call plbox3('bnstu','x axis', 0.0d0,
     &     0, 'bnstu', 'y axis', 0.0d0, 0,
     &     'bcdmnstuv','z axis', 0.0d0, 0)
         call plcol(2)
         call plot3d(x,y,z,npts,npts,opt(k),1,npts)
         call plcol(3)
         call plmtex('t', 1.0d0, 0.5d0, 0.5d0, title(k))

    5 continue
      call plend

      end
