c $Id$
c
c  Drawing "spirograph" curves - epitrochoids, cycolids, roulettes
c
c  Copyright (C) 2007  Arjen Markus
c
c This file is part of PLplot.
c
c PLplot is free software; you can redistribute it and/or modify
c it under the terms of the GNU Library General Public License as published
c by the Free Software Foundation; either version 2 of the License, or
c (at your option) any later version.
c
c PLplot is distributed in the hope that it will be useful,
c but WITHOUT ANY WARRANTY; without even the implied warranty of
c MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
c GNU Library General Public License for more details.
c
c You should have received a copy of the GNU Library General Public License
c along with PLplot; if not, write to the Free Software
c Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
c
c

c --------------------------------------------------------------------------
c main
c
c Generates two kinds of plots:
c   - construction of a cycloid (animated)
c   - series of epitrochoids and hypotrochoids
c --------------------------------------------------------------------------

      program x27f

      implicit none

      include 'plplot_parameters.h'


      integer i, j, fill

      real*8 params(4,9)
c     R, r, p, N
c     R and r should be integers to give correct termination of the
c     angle loop using gcd.
c     N.B. N is just a place holder since it is no longer used
c     (because we now have proper termination of the angle loop).
      data ( ( params(i,j) ,i=1,4) ,j=1,9 ) /
     & 21.0d0,  7.0d0,  7.0d0,  3.0d0,
     & 21.0d0,  7.0d0, 10.0d0,  3.0d0,
     & 21.0d0, -7.0d0, 10.0d0,  3.0d0,
     & 20.0d0,  3.0d0,  7.0d0, 20.0d0,
     & 20.0d0,  3.0d0, 10.0d0, 20.0d0,
     & 20.0d0, -3.0d0, 10.0d0, 20.0d0,
     & 20.0d0, 13.0d0,  7.0d0, 20.0d0,
     & 20.0d0, 13.0d0, 20.0d0, 20.0d0,
     & 20.0d0,-13.0d0, 20.0d0, 20.0d0/

c  plplot initialization

c  Parse and process command line arguments

      call plparseopts(PL_PARSE_FULL)

c  Initialize plplot

      call plinit()

c  Illustrate the construction of a cycloid

      call cycloid()

c  Loop over the various curves
c  First an overview, then all curves one by one

      call plssub(3, 3)

      fill = 0
      do 110 i = 1,9
          call pladv(0)
          call plvpor( 0.0d0, 1.0d0, 0.0d0, 1.0d0 )
          call spiro( params(1,i), fill )
  110 continue

      call pladv(0)
      call plssub(1, 1)

      do 120 i = 1,9
          call pladv(0)
          call plvpor( 0.0d0, 1.0d0, 0.0d0, 1.0d0 )
          call spiro( params(1,i), fill )
  120 continue

c     fill the curves.
      fill = 1
      call pladv(0)
      call plssub(1, 1)

      do 130 i = 1,9
          call pladv(0)
          call plvpor( 0.0d0, 1.0d0, 0.0d0, 1.0d0 )
          call spiro( params(1,i), fill )
  130 continue

c     Finally, an example to test out plarc capabilities

      call arcs()

      call plend()
      end

c  --------------------------------------------------------------------------
c Calculate greatest common divisor following pseudo-code for the
c Euclidian algorithm at http://en.wikipedia.org/wiki/Euclidean_algorithm

      integer function gcd (a,  b)
      implicit none
      integer a, b, t
      a = abs(a)
      b = abs(b)
      do while ( b .ne. 0 )
         t = b
         b = mod (a, b)
         a = t
      enddo
      gcd = a
      end

c  ===============================================================

      subroutine cycloid

c     TODO

      end

c  ===============================================================

      subroutine spiro( params, fill )

      implicit none
      include 'plplot_parameters.h'

      real*8      params(*)
      integer     NPNT
      parameter ( NPNT = 2000 )
      real*8      xcoord(NPNT+1)
      real*8      ycoord(NPNT+1)

      integer     windings
      integer     steps
      integer     i
      integer     fill
      real*8      phi
      real*8      phiw
      real*8      dphi
      real*8      xmin
      real*8      xmax
      real*8      xrange_adjust
      real*8      ymin
      real*8      ymax
      real*8      yrange_adjust
      integer gcd

c     Fill the coordinates

c     Proper termination of the angle loop very near the beginning
c     point, see
c     http://mathforum.org/mathimages/index.php/Hypotrochoid.
      windings = int(abs(params(2))/gcd(int(params(1)), int(params(2))))
      steps    = NPNT/windings
      dphi     = 2.d0*PI/dble(steps)

      do 110 i = 1,windings*steps+1
          phi       = dble(i-1) * dphi
          phiw      = (params(1)-params(2))/params(2)*phi
          xcoord(i) = (params(1)-params(2))*cos(phi)+params(3)*cos(phiw)
          ycoord(i) = (params(1)-params(2))*sin(phi)-params(3)*sin(phiw)

          if (i.eq.1) then
               xmin = xcoord(1)
               xmax = xcoord(1)
               ymin = ycoord(1)
               ymax = ycoord(1)
          endif
          if ( xmin > xcoord(i) ) xmin = xcoord(i)
          if ( xmax < xcoord(i) ) xmax = xcoord(i)
          if ( ymin > ycoord(i) ) ymin = ycoord(i)
          if ( ymax < ycoord(i) ) ymax = ycoord(i)
  110 continue

      xrange_adjust = 0.15d0 * (xmax - xmin)
      xmin = xmin - xrange_adjust
      xmax = xmax + xrange_adjust
      yrange_adjust = 0.15d0 * (ymax - ymin)
      ymin = ymin - yrange_adjust
      ymax = ymax + yrange_adjust

      call plwind( xmin, xmax, ymin, ymax )

      call plcol0(1)
      if (fill.eq.1) then
         call plfill( 1+steps*windings, xcoord, ycoord )
      else
         call plline( 1+steps*windings, xcoord, ycoord )
      endif

      end

c  ===============================================================

      subroutine arcs()
      
      implicit none
      include 'plplot_parameters.h'
      integer NSEG
      parameter ( NSEG = 8 )
      integer i;
      real*8 theta, dtheta
      real*8 a, b

      theta = 0.0d0
      dtheta = 360.0d0 / dble(NSEG)
      call plenv( -10.0d0, 10.0d0, -10.0d0, 10.0d0, 1, 0 )

c     Plot segments of circle in different colors
      do i = 0, NSEG-1
        call plcol0( mod(i,2) + 1 )
        call plarc(0.0d0, 0.0d0, 8.0d0, 8.0d0, theta, theta + dtheta, 
     1       0.0d0, 0)
        theta = theta + dtheta
      enddo
      
c     Draw several filled ellipses inside the circle at different
c     angles.
      a = 3.0d0
      b = a * tan( (dtheta/180.0d0*pi)/2.0d0 )
      theta = dtheta/2.0d0
      do i = 0, NSEG-1 
         call plcol0( 2 - mod(i,2) )
         call plarc( a*cos(theta/180.0d0*pi), a*sin(theta/180.0d0*pi), 
     1        a, b, 0.0d0, 360.0d0, theta, .true.)
         theta = theta + dtheta;
      enddo

      end
