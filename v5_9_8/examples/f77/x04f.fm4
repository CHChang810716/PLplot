c      $Id$
c      Illustration of logarithmic axes, and redefinition of window
c
c      Copyright (C) 2004  Alan W. Irwin
c
c      This file is part of PLplot.
c
c      PLplot is free software; you can redistribute it and/or modify
c      it under the terms of the GNU Library General Public License as
c      published by the Free Software Foundation; either version 2 of the
c      License, or (at your option) any later version.
c
c      PLplot is distributed in the hope that it will be useful,
c      but WITHOUT ANY WARRANTY; without even the implied warranty of
c      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
c      GNU Library General Public License for more details.
c
c      You should have received a copy of the GNU Library General Public
c      License along with PLplot; if not, write to the Free Software
c      Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA

      implicit none
      include 'plplot_parameters.h'
c      Process command-line arguments
      call plparseopts(PL_PARSE_FULL)

      call plinit
      call plfont(2)
c      Make log plots using two different styles.
      call plot1(0)
      call plot1(1)
      call plend
      end

      subroutine plot1(type)
      implicit none
      include 'plplot_parameters.h'
      real*8 freql(0:100),ampl(0:100),phase(0:100), freq, f0
      integer i, type

      real*8 legend_width, legend_height
      integer nlegend
      integer opt_array(2), text_colors(2), line_colors(2),
     &        line_styles(2), line_widths(2), symbol_colors(2),
     &        symbol_numbers(2)
      real*8 symbol_scales(2)
      integer box_colors(1), box_patterns(1), box_line_widths(1)
      real*8 box_scales(1)
      character*20 text(2)
      character*1 symbols(2)

      call pladv(0)
c      Set up data for log plot.
      f0 = 1.d0
      do i=0,100
        freql(i)= -2.0d0 + dble (i)/20.0d0
        freq=10.0d0**freql(i)
        ampl(i)=20.0d0*log10(1.0d0/sqrt(1.0d0+(freq/f0)**2))
        phase(i)=-(180.0d0/PI)*atan(freq/f0)
      enddo
      call plvpor(0.15d0, 0.85d0, 0.1d0, 0.9d0)
      call plwind(-2.0d0, 3.0d0, -80.0d0, 0.0d0)
      call plcol0(1)
c      Try different axis and labelling styles.
      if (type.eq.0) then
        call plbox('bclnst', 0.0d0, 0, 'bnstv', 0.0d0, 0)
      elseif (type.eq.1) then
        call plbox('bcfghlnst', 0.0d0, 0, 'bcghnstv', 0.0d0, 0)
      else
        stop 'plot1: invalid type'
      endif
c      Plot ampl vs freq.
      call plcol0(2)
      call plline(101,freql,ampl)
      call plcol0(2)
      call plptex(1.6d0, -30.0d0, 1.0d0, -20.0d0, 0.5d0,
     &            '-20 dB/decade')
c      Put labels on.
      call plcol0(1)
      call plmtex('b', 3.2d0, 0.5d0, 0.5d0, 'Frequency')
      call plmtex('t', 2.0d0, 0.5d0, 0.5d0,
     &            'Single Pole Low-Pass Filter')
      call plcol0(2)
      call plmtex('l', 5.0d0, 0.5d0, 0.5d0, 'Amplitude (dB)')
      nlegend = 1
c      For the gridless case, put phase vs freq on same plot.
      if(type.eq.0) then
        call plcol0(1)
        call plwind(-2.0d0, 3.0d0, -100.0d0, 0.0d0)
        call plbox(' ', 0.0d0, 0, 'cmstv', 30.0d0, 3)
        call plcol0(3)
        call plline(101,freql,phase)
        call plstring(101,freql,phase,'*')
        call plcol0(3)
        call plmtex('r', 5.0d0, 0.5d0, 0.5d0,
     &              'Phase shift (degrees)')
        nlegend = 2
      endif
c      Draw a legend
c      First legend entry.
      opt_array(1)   = PL_LEGEND_LINE
      text_colors(1) = 2
      text(1)        = 'Amplitude'
      line_colors(1) = 2
      line_styles(1) = 1
      line_widths(1) = 1
c      note from the above opt_array the first symbol (and box) indices
c      do not have to be specified

c      Second legend entry.
      opt_array(2)      = PL_LEGEND_LINE + PL_LEGEND_SYMBOL
      text_colors(2)    = 3
      text(2)           = 'Phase shift'
      line_colors(2)    = 3
      line_styles(2)    = 1
      line_widths(2)    = 1
      symbol_colors(2)  = 3
      symbol_scales(2)  = 1.0
      symbol_numbers(2) = 4
      symbols(2)        = '*'
c      from the above opt_arrays we can completely ignore everything
c      to do with boxes.

      call plscol0a( 15, 32, 32, 32, 0.70d0 )
      call pllegend( legend_width, legend_height,
     &    PL_LEGEND_BACKGROUND + PL_LEGEND_BOUNDING_BOX, 0,
     &    0.0d0, 0.0d0, 0.1d0, 15,
     &    1, 1, 0, 0,
     &    nlegend, opt_array,
     &    1.0d0, 1.0d0, 2.0d0,
     &    1.0d0, text_colors, text,
     &    box_colors, box_patterns, box_scales, box_line_widths,
     &    line_colors, line_styles, line_widths,
     &    symbol_colors, symbol_scales, symbol_numbers, symbols )

      end
